# source this file to get rid of the NEMO environment
#
if ($?NEMOBIN == 1) then
  set tmp=()
  foreach p ($path)
    if ($p != $NEMOBIN) then
      set tmp=($tmp $p)
    endif
  end
  set path=($tmp)
  rehash
  echo $NEMOBIN removed from search path
endif

# provide alias to reload this version of NEMO again
if ($?NEMO) then
 #alias nemo   'source $NEMO/nemo_start'
 alias nemo   "source $NEMO/nemo_start"
endif

if ($?FALCON) then
  source $FALCON/falcON_end
endif

# now remove all NEMO and YAPP env.variables
foreach evar (`printenv|awk -F= '{print $1}'|egrep '(NEMO|YAPP)'`)
  unsetenv $evar
end
