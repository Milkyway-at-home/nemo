#! /bin/csh -f
#
# example script which installs NEMO from scratch. Needs (CVS) network access 
# see also $NEMO/AAA_SOURCE_ME
#
#
#  P500/     431.720u 58.010s 10:52.41 75.0% 0+0k 0+0io 1841259pf+0w
# P1600/     209.350u 33.580s  5:11.70 77.9% 0+0k 0+0io 1712038pf+0w
# P2800/     113.260u 16.760s  2:45.88 78.3% 0+0k 0+0io 1993802pf+0w

set pgplot=1
set root=nemo
  
if ($?NEMO) then
  echo Cannot test if NEMO=$NEMO is present already
  exit 1
endif

if (1) then
 setenv CVSROOT :pserver:anonymous@cvs.astro.umd.edu:/home/cvsroot
endif

#		pick another GNU compiler
if (0) then
    source /astromake/astromake_start
    astroload -v 3.2.1 gcc
endif

#		pick intel compiler
if (0) then
  source /astromake/astromake_start
  astroload intel
  setenv CC icc
  setenv F77 ifc
endif

#		pick a certain CVS release
if (0) then
  set cvs_rev=(-r nemo_3_0_16)
else
  set cvs_rev=()
endif

#		get  the source ($pgplot is done below)
echo Getting the NEMO source code via CVS into $root
time cvs -Q checkout $cvs_rev -d $root nemo

# --disable-gsl
# --enable-single

cd $root
#./configure --with-pgplot-prefix=/astromake/opt/pgplot --with-yapp=pgplot 
#./configure --with-pgplot-prefix=/astromake/opt/pgplot
#./configure --with-yapp=ps 
./configure --with-yapp=pgplot 
source nemo_start
if ($pgplot) then
  make dirs config_clean
  rm NEMORC.local
  echo Getting the PGPLOT source code via CVS into NEMO/local/pgplot
  (cd local; cvs -Q checkout pgplot)
  (cd src/scripts; ./pgplot.install dir=$NEMO/local/pgplot) >& pgplot.install.log
  setenv PGPLOT_DIR $NEMOLIB
  ./configure --with-pgplot-prefix=$NEMOLIB --with-yapp=pgplot
endif
make dirs config_extra scripts
source NEMORC.local
rehash
(cd man; ./make_html)
make libs
src/scripts/testsuite -b

if (1) then
if (-d usr/dehnen/falcON) then
  # falcON
  set fbins=(TestGrav gyrfalcON addgravity setgravity)
  set arch=i386_linux
  echo Compiling $fbins for $arch
  ( cd usr/dehnen/falcON; make ; make $fbins; cd $arch; cp $fbins $NEMOBIN)  >& install_falcON.log
else
  # YancNemo
  echo Compiling YancNemo
  ( cd usr/dehnen/yanc; mkdir -p lib; make ; make YancNemo mkking; cp YancNemo mkking $NEMOBIN )  >& install_yanc.log
endif
endif

# src/scripts/nemo.vogl

echo All done.
